plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
    id 'java-library'
    id 'idea'
    id "com.modrinth.minotaur" version "2.+"
}

repositories {
    maven { url 'https://api.modrinth.com/maven' }
}

apply plugin: 'com.modrinth.minotaur'
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "async"
    versionNumber.set(project.version.toString())
    versionName.set(project.version.toString())
    versionType = "alpha"
    def libsDir = layout.buildDirectory.dir("libs").get().asFile
    uploadFile = libsDir.listFiles().find { it.name.endsWith(".jar") }
    gameVersions = ["1.21.9", "1.21.10"]
    loaders = ["neoforge"]
    dependencies {
        optional.project "lithium"
        optional.project "c2me-neoforge"
        incompatible.project "moonrise-opt"
    }
    changelog = """
"""
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    runs {
        configureEach {
            systemProperty("neoforge.enabledGameTestNamespaces", mod_id.toString())
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
    if (project.hasProperty('parchment_mappings_version') && project.hasProperty('parchment_minecraft_version')) {
        def parchmentMappings = project.parchment_mappings_version
        def parchmentMinecraft = project.parchment_minecraft_version

        if (parchmentMappings != null && parchmentMinecraft != null &&
                !parchmentMappings.toString().isBlank() && !parchmentMinecraft.toString().isBlank()) {

            parchment {
                mappingsVersion = parchmentMappings
                minecraftVersion = parchmentMinecraft
            }
        }
    }
}

dependencies {
    compileOnly(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:${mixinsquared_version}"))
    implementation(jarJar("com.github.bawnorton.mixinsquared:mixinsquared-neoforge:${mixinsquared_version}"))
    implementation 'maven.modrinth:lithium:mc1.21.9-0.19.1-neoforge'
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }