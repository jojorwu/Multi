plugins {
    id 'net.neoforged.moddev'
    id 'idea'
}

neoForge {
    neoFormVersion = neoform_version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    if (project.hasProperty('parchment_mappings_version') && project.hasProperty('parchment_minecraft_version')) {
        def parchmentMappings = project.parchment_mappings_version
        def parchmentMinecraft = project.parchment_minecraft_version

        if (parchmentMappings != null && parchmentMinecraft != null &&
                !parchmentMappings.toString().isBlank() && !parchmentMinecraft.toString().isBlank()) {

            parchment {
                mappingsVersion = parchmentMappings
                minecraftVersion = parchmentMinecraft
            }
        }
    }
}

repositories {
    maven { url = 'https://maven.bawnorton.com/releases' }
    maven { url 'https://api.modrinth.com/maven' }
}

dependencies {
    implementation("io.github.llamalad7:mixinextras-common:${mixinextras_version}")
    annotationProcessor("io.github.llamalad7:mixinextras-common:${mixinextras_version}")
    compileOnly("org.spongepowered:mixin:${mixin_version}")
    implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:${mixinsquared_version}"))
    implementation("org.ow2.asm:asm:9.6")
    implementation("org.ow2.asm:asm-tree:9.6")
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    implementation 'maven.modrinth:lithium:mc1.21.9-0.19.1-neoforge'
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}
